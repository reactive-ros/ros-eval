apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'idea'

repositories {		
	mavenCentral()
	maven {
		url "https://github.com/rosjava/rosjava_mvn_repo/raw/master/"
	}
	maven { url "https://jitpack.io" }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = "com.github.rhea-flow"
version = 1.0

configurations.all {
    // check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}
dependencies {	
	// rhea-flow
	compile 'com.github.rhea-flow:rhea-core:-SNAPSHOT'

	// ros
	compile 'org.ros.rosjava_core:rosjava:0.2.1'

	// Serialization
	compile 'com.cedarsoftware:json-io:4.1.9'

	// Testing
	testCompile 'junit:junit:4.11'
	testCompile 'com.github.rhea-flow:rx-eval:-SNAPSHOT'
	testCompile 'com.github.rhea-flow:test-data:-SNAPSHOT'
	testCompile 'com.github.rhea-flow:hazelcast-distribution:-SNAPSHOT'
	testCompile 'org.ros.rosjava_messages:sensor_msgs:1.11.7'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

test {
	testLogging {
		displayGranularity 3
		//events 'passed', 'failed', 'standard_out'
		//events 'passed', 'failed', 'standard_error'
		events 'passed', 'failed', 'standard_out', 'standard_error'
	}
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
      tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
      }
    }
  }

task wrapper(type: Wrapper) {
	gradleVersion = '2.3'
}